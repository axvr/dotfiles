" ~/.vim/vimrc

filetype plugin indent on
if !exists('g:syntax_on')
    syntax enable
endif

" Essentials
set startofline hidden autoread display=truncate
set backspace=indent,eol,start nrformats=bin,hex,unsigned
set showmatch showcmd ruler history=10000 shortmess=ltToOCF
set wildmenu wildignore+=*/.git/*,*/node_modules/*,tags,.DS_Store,*/.cpcache/*
set mouse=a belloff=all scrolloff=0 sidescroll=1 nofoldenable
set hlsearch incsearch ttimeout ttimeoutlen=50
set formatoptions+=jpl1n cpoptions+=J joinspaces
set grepformat^=%f:%l:%c:%m   " Match column numbers in 'grepformat'
set expandtab tabstop=8 softtabstop=4 shiftwidth=4
set shiftround autoindent nosmarttab
set wrap showbreak=>>>\  linebreak breakindent
set path=.,,** clipboard=unnamed
set spelllang=en_gb dictionary=/usr/dict/words,/usr/share/dict/words
set sessionoptions+=slash,unix sessionoptions-=options
set viewoptions+=slash,unix viewoptions-=options

" Backup, swap and undo
" TODO: unify these between editors.
if has('nvim')
    set backupdir-=.
else
    let &backupdir = expand($HOME.'/.vim/state/backup')
    let &directory = expand($HOME.'/.vim/state/swap')
    let &undodir   = expand($HOME.'/.vim/state/undo')
endif
set backup undofile
for s:dir in [&backupdir, &directory, &undodir]
    if !isdirectory(s:dir)
        call mkdir(s:dir, 'p')
    endif
endfor
unlet s:dir

" "<Leader>" and "<LocalLeader>" prefixes
let g:mapleader = " "
let g:maplocalleader = "\\"

" Theming
if &term =~# '256color$' || has('gui')
    set termguicolors
    colorscheme raider
    let &colorcolumn='+'.join(range(1,256),',+')
endif
if &term =~# '^\(screen\|tmux\)'
    " :help xterm-true-color
    let &t_8f = "\<Esc>[38;2;%lu;%lu;%lum"
    let &t_8b = "\<Esc>[48;2;%lu;%lu;%lum"
endif

" Enable blinking cursor
set guicursor=n-v-c:block,i-ci-ve:ver25,r-cr:hor20,o:hor50
            \,a:blinkwait700-blinkoff400-blinkon250-Cursor/lCursor
            \,sm:block-blinkwait175-blinkoff150-blinkon175

" Sensible default mappings
map Q gq | sunmap Q
inoremap <C-U> <C-G>u<C-U>

if has('nvim') && exists(':menu')
    aunmenu PopUp.-1-
    aunmenu PopUp.How-to\ disable\ mouse
endif

" Create parent directories on buffer write if they don't exist.
function! s:create_parent_dirs()
    let dir = expand("%:p:h")
    if ! isdirectory(dir) && confirm('Create directory "'.dir.'"?', "&Yes\n&No", 0, 'Question') == 1
        call mkdir(dir, 'p')
    endif
endfunction

function! s:trim_whitespace()
    if ! get(b:, 'no_whitespace_trim')
        let view = winsaveview()
        keeppatterns %s/\s\+$//e
        call winrestview(view)
    endif
endfunction

augroup file_utils
    autocmd!
    autocmd BufWritePre * call s:create_parent_dirs()
    autocmd BufWritePre * call s:trim_whitespace()
    " Don't trim whitespace on diff files as it breaks syntax highlighting.
    autocmd FileType gitcommit,diff let b:no_whitespace_trim = 1
augroup END

augroup cursor_restore
    autocmd!
    " When editing a file, jump to the last known cursor position.
    autocmd BufReadPost * if line("'\"") >= 1 && line("'\"") <= line("$") | exe "normal! g`\"" | endif
    " Reset cursor position to top of file for these patterns.
    autocmd BufReadPost */.git/* normal! gg0
augroup END

augroup filetype_config
    autocmd!

    autocmd FileType c,cpp,go,gitconfig,fstab setl noet sts=8 sw=8
    autocmd FileType lisp,clojure,scheme,json,ruby,markdown setl et sts=2 sw=2
    autocmd FileType html,css setl noet sts=2 sw=2 ts=2
    autocmd FileType perl,sh,python,javascript setl tw=79
    autocmd FileType lisp,clojure,scheme setl commentstring=;;%s cpo-=J
    autocmd FileType robots,crontab,spec,desktop setl commentstring=#%s
    autocmd FileType c,cpp setl path+=/usr/include
    autocmd FileType tex compiler latexmk
    autocmd FileType jsonl setl nowrap
    autocmd FileType gitcommit setl spell
    autocmd FileType git,gitcommit,diff setl foldmethod=syntax foldlevel=100
    autocmd FileType sh,bash compiler shellcheck
    autocmd FileType sh,bash setl keywordprg=:Man

    autocmd BufRead,BufNewFile .bash{rc,_profile,_completion,_logout} setl ft=bash
    autocmd BufRead,BufNewFile *.prolog,*.pro,*.PRO,*.pg setfiletype prolog
    autocmd BufRead,BufNewFile TODO,DOING,DONE setl ft=markdown
    " Redo <https://cr.yp.to/redo.html> <http://news.dieweltistgarnichtso.net/bin/redo-sh.html>
    autocmd BufRead,BufNewFile *.do setl filetype=sh
augroup END
